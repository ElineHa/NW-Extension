# Ideally, for each primitive, we should think about cases such as:
# - Parameter values: negative, 0, 1, possible maximums (e.g. 1E38)
# - same input twice (e.g. distance to self)
# Different network configurations:
# - empty networks
# - partially disconnected networks
# - completely disconnect networks
# - Directed vs. undirected networks
#   - using undirected primitives with directed networks
#   - using directed versions of primitives on undirected networks
# For primitives using weights:
# - non numeric weights
# - negative weights

version-number
  extensions [nw]
  nw:version => "1.0.0-RC3"

### set-context tests

clear-all-clears-context
  extensions [nw]
  O> crt 2 [ create-links-with other turtles ]
  O> nw:set-context (turtle-set turtles) (link-set links)
  (word map sort nw:get-context) => "[[(turtle 0) (turtle 1)] [(link 0 1)]]"
  O> clear-all
  (map sort nw:get-context) => [[] []]

distance-after-changes
  extensions [nw]
  O> crt 2
  O> ask turtle 0 [ create-link-with turtle 1 ]
  [ nw:distance-to turtle 1 ] of turtle 0 => 1
  O> ask links [ die ]
  [ nw:distance-to turtle 1 ] of turtle 0 => false
  O> crt 1
  O> ask turtle 0 [ create-link-with turtle 2 ]
  O> ask turtle 2 [ create-link-with turtle 1 ]
  [ nw:distance-to turtle 1 ] of turtle 0 => 2

closeness-after-births
  extensions [ nw ]
  O> crt 1
  [ nw:closeness-centrality ] of turtle 0 => 0
  O> crt 2
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ create-link-with turtle 2 ]
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => [0.67 1 0.67]

closeness-after-deaths
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ create-link-with turtle 2 ]
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => [0.67 1 0.67]
  O> ask turtle 1 [ die ]
  O> ask turtle 0 [ create-link-with turtle 2 ]
  [ nw:closeness-centrality ] of turtles => [1 1]

default-context-with-undir-links-betweenness
  extensions [nw]
  O> crt 2 [ create-undirected-link-with one-of other turtles ]
  [ nw:betweenness-centrality ] of turtles => [0 0]

undir-context-with-undir-links-betweenness
  extensions [nw]
  O> nw:set-context turtles undirected-links
  O> crt 2 [ create-undirected-link-with one-of other turtles ]
  [ nw:betweenness-centrality ] of turtles => [0 0]

default-context-with-undir-links-closeness
  extensions [nw]
  O> crt 2 [ create-undirected-link-with one-of other turtles ]
  [ nw:closeness-centrality ] of turtles => [1 1]

default-context-with-undir-links-distance
  extensions [nw]
  O> crt 2 [ create-undirected-link-with one-of other turtles ]
  [ nw:distance-to turtle 1 ] of turtle 0 => 1


after-clear-turtles
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles ]
  [ nw:distance-to one-of other turtles ] of one-of turtles => 1
  O> clear-turtles
  O> crt 2 [ create-links-with other turtles ]
  [ nw:distance-to one-of other turtles ] of one-of turtles => 1

after-clear-turtles-agentset
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles ]
  O> nw:set-context (turtle-set turtles) links
  [ nw:distance-to one-of other turtles ] of one-of turtles => 1
  O> clear-turtles
  O> crt 2 [ create-links-with other turtles ]
  [ nw:distance-to one-of other turtles ] of one-of turtles => false

after-world-resize_2D
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles ]
  [ nw:distance-to one-of other turtles ] of one-of turtles => 1
  O> resize-world -1 1 -1 1
  O> crt 2 [ create-links-with other turtles ]
[ nw:distance-to one-of other turtles ] of one-of turtles => 1

after-world-resize_3D
  extensions [ nw ]
  O> crt 2
  O> ask turtle 0 [ create-link-with turtle 1 ]
  [ nw:distance-to one-of other turtles ] of one-of turtles => 1
  O> resize-world -1 1 -1 1 -1 1
  O> crt 2
  O> ask turtle 2 [ create-link-with turtle 3 ]
  [ nw:distance-to one-of other turtles ] of one-of turtles => 1

## set-context with non-breed agentsets

path-through-different-breeds
  extensions [ nw ]
  O> create-frogs 1
  O> create-mice 2 [ create-undirected-links-with frogs ]
  O> ask undirected-links [ set weight 2 ]
  O> nw:set-context (turtle-set mice frogs) (link-set undirected-links)
  [ nw:distance-to mouse 2 ] of mouse 1 => 2
  [ nw:weighted-distance-to mouse 2 "weight" ] of mouse 1 => 4
  O> ask frogs [ die ]
  [ nw:distance-to mouse 2 ] of mouse 1 => false
  [ nw:weighted-distance-to mouse 2 "weight" ] of mouse 1 => false
  O> create-frogs 1
  O> create-mice 2 [ create-undirected-link-with frog 3 ]
  # new agents not added to non-breed context:
  [ nw:distance-to mouse 4 ] of mouse 5 => false

after-world-resize-non-breed_2D
  extensions [ nw ]
  O> create-frogs 1
  O> create-mice 2 [ create-undirected-link-with frog 0 ]
  O> nw:set-context (turtle-set mice frogs) (link-set undirected-links)
  [ nw:distance-to mouse 2 ] of mouse 1 => 2
  O> resize-world -1 1 -1 1
  O> create-frogs 1
  O> create-mice 2 [ create-undirected-link-with frog 3 ]
  # new agents should not be added to context:
  [ nw:distance-to mouse 4 ] of mouse 5 => false

after-world-resize-non-breed_3D
  extensions [ nw ]
  O> create-frogs 1
  O> create-mice 2 [ create-undirected-link-with frog 0 ]
  O> nw:set-context (turtle-set mice frogs) (link-set undirected-links)
  [ nw:distance-to mouse 2 ] of mouse 1 => 2
  O> resize-world -1 1 -1 1 -1 1
  O> create-frogs 1
  O> create-mice 2 [ create-undirected-link-with frog 3 ]
  # new agents should not be added to context:
  [ nw:distance-to mouse 4 ] of mouse 5 => false

closeness-after-births-non-breed
  extensions [ nw ]
  O> create-frogs 1
  O> create-mice 2 [ create-undirected-link-with frog 0 ]
  O> nw:set-context (turtle-set mice frogs) (link-set undirected-links)
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => [1 0.67 0.67]
  O> crt 1
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => ERROR Extension exception: turtle 3 is not a member of the current graph context.
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles with [ who < 3 ] => [1 0.67 0.67]

closeness-after-deaths-non-breed
  extensions [ nw ]
  O> create-frogs 1
  O> create-mice 2 [ create-undirected-link-with frog 0 ]
  O> nw:set-context (turtle-set mice frogs) (link-set undirected-links)
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => [1 0.67 0.67]
  O> ask frogs [ die ]
  # create new link that should not be part of context:
  O> ask mouse 1 [ create-link-with mouse 2 ]
  [ nw:closeness-centrality ] of turtles => [0 0]

### with-context tests
with-context-actually-runs-body
  extensions [ nw ]
  O> nw:with-context turtles links [ set glob1 5 ]
  glob1 => 5

with-context-changes-context-temporarily
  extensions [ nw ]
  O> create-turtles 2 [ create-links-with other turtles ]
  O> set glob1 turtle-set turtles
  O> create-turtles 1 [ create-links-with other turtles ]
  [ count nw:turtles-in-radius 1 ] of turtle 0 => 3
  O> nw:with-context glob1 links [ set glob2 [ nw:turtles-in-radius 1 ] of turtle 0 ]
  count glob2 => 2
  [ member? turtle 0 nw:turtles-in-radius 1 ] of turtle 0 => true
  [ member? turtle 1 nw:turtles-in-radius 1 ] of turtle 0 => true
  [ member? turtle 2 nw:turtles-in-radius 1 ] of turtle 0 => true
  [ count nw:turtles-in-radius 1 ] of turtle 0 => 3

with-context-can-be-nested
  extensions [ nw ]
  O> crt 3
  O> ask turtles [ create-links-with other turtles ]
  O> nw:with-context (turtle-set turtle 0 turtle 1) links [ nw:with-context (turtle-set turtle 0) links [ set glob1 [ nw:turtles-in-radius 1 ] of turtle 0 ] set glob2 [ nw:turtles-in-radius 1 ] of turtle 0 ]
  count glob1 => 1
  count glob2 => 2
  count [ nw:turtles-in-radius 1 ] of turtle 0 => 3

with-context-changes-get-context
  extensions [ nw ]
  O> crt 3
  O> ask turtles [ create-links-with other turtles ]
  O> set glob1 (turtle-set turtle 0 turtle 1)
  O> set glob2 (link-set link 0 1)
  O> nw:with-context glob1 glob2 [ set glob3 nw:get-context ]
  glob3 = (list glob1 glob2) => true
  nw:get-context = (list turtles links) => true

### turtles-in-radius

turtles-in-radius-negative-radius
  extensions [nw]
  O> crt 1
  [nw:turtles-in-radius -1] of turtle 0 => ERROR Extension exception: radius cannot be negative

turtles-in-radius-zero-radius
  extensions [nw]
  O> crt 5
  remove-duplicates [count nw:turtles-in-radius 0] of turtles => [1]

turtles-in-radius-two-circles
  extensions [nw]
  O> crt 8 [ create-link-with turtle ((who + 1) mod 8) ]
  O> crt 8 [ create-link-with turtle ((who - 8 + 1) mod 8 + 8) ]
  remove-duplicates [count nw:turtles-in-radius 0] of turtles => [1]
  remove-duplicates [count nw:turtles-in-radius 1 ] of turtles => [3]
  remove-duplicates [count nw:turtles-in-radius 2 ] of turtles => [5]
  remove-duplicates [count nw:turtles-in-radius 3 ] of turtles => [7]
  remove-duplicates [count nw:turtles-in-radius 4 ] of turtles => [8]
  remove-duplicates [count nw:turtles-in-radius 5 ] of turtles => [8]
  remove-duplicates [count nw:turtles-in-radius 1E38 ] of turtles => [8]

in-link-radius-source-set-filtering
  extensions [nw]
  O> crt 8 [ create-links-with other turtles ]
  O> ask turtles with [who mod 2 = 0] [ set breed mice ]
  O> nw:set-context mice links
  sort [who] of [nw:turtles-in-radius 10] of turtle 0 => [0 2 4 6]

mixed-turtles-in-radius
  extensions [nw]
  O> cro 13
  O> ask turtle 0 [ create-undirected-link-with turtle 1 ]
  O> ask turtle 0 [ create-directed-link-to turtle 2 ]
  O> ask turtle 0 [ create-directed-link-from turtle 3 ]
  O> ask turtle 1 [ create-undirected-link-with turtle 4 ]
  O> ask turtle 1 [ create-directed-link-to turtle 5 ]
  O> ask turtle 1 [ create-directed-link-from turtle 6 ]
  O> ask turtle 2 [ create-undirected-link-with turtle 7 ]
  O> ask turtle 2 [ create-directed-link-to turtle 8 ]
  O> ask turtle 2 [ create-directed-link-from turtle 9 ]
  O> ask turtle 3 [ create-undirected-link-with turtle 10 ]
  O> ask turtle 3 [ create-directed-link-to turtle 11 ]
  O> ask turtle 3 [ create-directed-link-from turtle 12 ]
  sort [who] of [nw:turtles-in-radius 1] of turtle 0 => [0 1 2]
  sort [who] of [nw:turtles-in-reverse-radius 1] of turtle 0 => [0 1 3]
  sort [who] of [nw:turtles-in-radius 2] of turtle 0 => [0 1 2 4 5 7 8]
  sort [who] of [nw:turtles-in-reverse-radius 2] of turtle 0 => [0 1 3 4 6 10 12]
  O> nw:set-context turtles undirected-links
  sort [who] of [nw:turtles-in-radius 1] of turtle 0 => [0 1]
  sort [who] of [nw:turtles-in-radius 2] of turtle 0 => [0 1 4]
  O> nw:set-context turtles directed-links
  sort [who] of [nw:turtles-in-radius 1] of turtle 0 => [0 2]
  sort [who] of [nw:turtles-in-radius 2] of turtle 0 => [0 2 8]
  sort [who] of [nw:turtles-in-reverse-radius 1] of turtle 0 => [0 3]
  sort [who] of [nw:turtles-in-reverse-radius 2] of turtle 0 => [0 3 12]

### turtles-in-out-radius, turtles-in-in-radius

turtles-in-out-radius
  extensions [nw]
  O> crt 2
  O> ask turtle 0 [ create-link-to turtle 1 ]
  sort [ who ] of [ nw:turtles-in-radius 1 ] of turtle 0 => [0 1]
  sort [ who ] of [ nw:turtles-in-radius 1 ] of turtle 1 => [1]

turtles-in-in-radius
  extensions [nw]
  O> crt 2
  O> ask turtle 0 [ create-link-to turtle 1 ]
  sort [who] of [ nw:turtles-in-reverse-radius 1 ] of turtle 0 => [0]
  sort [who] of [ nw:turtles-in-reverse-radius 1 ] of turtle 1 => [0 1]

### distance-to

distance-to-self
  extensions [nw]
  O> crt 1
  [ nw:distance-to turtle 0 ] of turtle 0 => 0

distance-two-turtles-no-links
  extensions [nw]
  O> crt 2
  [ nw:distance-to turtle 0 ] of turtle 1 => false
  [ nw:distance-to turtle 1 ] of turtle 0 => false

distance-one-undirected-link
  extensions [nw]
  O> crt 2 [ create-links-with other turtles ]
  [ nw:distance-to turtle 0 ] of turtle 1 => 1
  [ nw:distance-to turtle 1 ] of turtle 0 => 1

distance-one-directed-link
  extensions [nw]
  O> crt 2
  O> ask turtle 0 [ create-link-to turtle 1 ]
  [ nw:distance-to turtle 0 ] of turtle 1 => false
  [ nw:distance-to turtle 1 ] of turtle 0 => 1

distance-always-one-in-fully-connected-network
  extensions [ nw ]
  to-report distances report [ nw:distance-to myself ] of link-neighbors end
  O> crt 10 [ create-links-with other turtles ]
  remove-duplicates reduce sentence [distances] of turtles => [1]

distance-along-chain
  extensions [ nw ]
  O> crt 5 [ if who < 4 [ create-link-with turtle (who + 1) ] ]
  sort [ nw:distance-to turtle 0 ] of turtles => [0 1 2 3 4]
  sort [ nw:distance-to turtle 4 ] of turtles => [0 1 2 3 4]
  sort remove-duplicates [ nw:distance-to turtle 2 ] of turtles => [0 1 2]

distance-around-circle
  extensions [ nw ]
  O> crt 8 [ create-link-with turtle ((who + 1) mod 8) ]
  sort [ nw:distance-to turtle 0 ] of turtles => [0 1 1 2 2 3 3 4]

distance-around-two-circles
  extensions [ nw ]
  O> crt 8 [ create-link-with turtle ((who + 1) mod 8) ]
  O> crt 8 [ create-link-with turtle ((who - 8 + 1) mod 8 + 8) ]
  sort [ nw:distance-to turtle 0 ] of turtles with [who < 8] => [0 1 1 2 2 3 3 4]
  sort [ nw:distance-to turtle 8 ] of turtles with [who >= 8] => [0 1 1 2 2 3 3 4]
  remove-duplicates [ nw:distance-to turtle 8 ] of turtles with [who < 8] => [false]
  remove-duplicates [ nw:distance-to turtle 0 ] of turtles with [who >= 8] => [false]

### path-to

path-to-self
  extensions [ nw ]
  O> crt 1
  [ nw:path-to turtle 0 ] of turtle 0 => []

path-to-no-path
  extensions [ nw ]
  O> crt 2
  [ nw:path-to turtle 1 ] of turtle 0 => []
  [ nw:path-to turtle 0 ] of turtle 1 => []

path-to-undirected-pair
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles ]
  (word [ nw:path-to turtle 1 ] of turtle 0) => "[(link 0 1)]"
  (word [ nw:path-to turtle 0 ] of turtle 1) => "[(link 0 1)]"

path-to-a-b-c-undirected
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ create-link-with turtle 2 ]
  (word [ nw:path-to turtle 2 ] of turtle 0) => "[(link 0 1) (link 1 2)]"
  (word [ nw:path-to turtle 0 ] of turtle 2) => "[(link 1 2) (link 0 1)]"

path-to-a-b-c-directed
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-link-to turtle 1 ]
  O> ask turtle 1 [ create-link-to turtle 2 ]
  (word [ nw:path-to turtle 2 ] of turtle 0) => "[(link 0 1) (link 1 2)]"
  (word [ nw:path-to turtle 0 ] of turtle 2) => "[]"

path-to-fully-connected
  extensions [ nw ]
  O> crt 6 [ create-links-with other turtles ]
  (word sort map [(word ?)] [ nw:path-to turtle 0 ] of turtles) => "[[(link 0 1)] [(link 0 2)] [(link 0 3)] [(link 0 4)] [(link 0 5)] []]"

path-to-on-a-circle
  extensions [ nw ]
  O> crt 6 [ create-link-with turtle ((who + 1) mod 6) ]
  sort [ length nw:path-to turtle 0 ] of turtles => [0 1 1 2 2 3]

path-to-random-equal-paths
  extensions [ nw ]
  O> cro 4 [ create-link-with turtle ((who + 1) mod 4) ]
  length remove-duplicates n-values 100 [ [ nw:path-to turtle 2 ] of turtle 0 ] => 2

path-to-random-equal-paths-parallel-links
  extensions [ nw ]
  O> cro 3
  O> ask turtle 0 [ create-undirected-link-with turtle 1 ]
  O> ask turtle 1 [ create-undirected-link-with turtle 2 ]
  O> ask turtle 1 [ create-directed-link-to turtle 2 ]
  length remove-duplicates n-values 100 [ [ nw:path-to turtle 2 ] of turtle 0 ] => 2

path-to-mixed-directed-undirected
  extensions [ nw ]
  O> crt 4
  O> ask turtle 0 [ create-undirected-link-with turtle 1 ]
  O> ask turtle 1 [ create-directed-link-to turtle 2 ]
  O> ask turtle 3 [ create-directed-link-to turtle 1 ]
  (word [ nw:path-to turtle 2 ] of turtle 0) => "[(undirected-link 0 1) (directed-link 1 2)]"
  (word [ nw:turtles-on-path-to turtle 2 ] of turtle 0) => "[(turtle 0) (turtle 1) (turtle 2)]"
  (word [ nw:path-to turtle 3 ] of turtle 0) => "[]"
  (word [ nw:turtles-on-path-to turtle 3 ] of turtle 0) => "[]"
  (word [ nw:path-to turtle 0 ] of turtle 3) => "[(directed-link 3 1) (undirected-link 0 1)]"
  (word [ nw:turtles-on-path-to turtle 0 ] of turtle 3) => "[(turtle 3) (turtle 1) (turtle 0)]"

### turtles-on-path-to

turtles-on-path-to-self
  extensions [ nw ]
  O> crt 1
  map [[who] of ?] [ nw:turtles-on-path-to turtle 0 ] of turtle 0 => [0]

turtles-on-path-to-no-path
  extensions [ nw ]
  O> crt 2
  map [[who] of ?] [ nw:turtles-on-path-to turtle 1 ] of turtle 0 => []
  map [[who] of ?] [ nw:turtles-on-path-to turtle 0 ] of turtle 1 => []

turtles-on-path-to-undirected-pair
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles ]
  map [[who] of ?] [ nw:turtles-on-path-to turtle 1 ] of turtle 0 => [0 1]
  map [[who] of ?] [ nw:turtles-on-path-to turtle 0 ] of turtle 1 => [1 0]

turtles-on-path-to-directed-pair
  extensions [ nw ]
  O> crt 2
  O> ask turtle 0 [ create-link-to turtle 1 ]
  map [[who] of ?] [ nw:turtles-on-path-to turtle 1 ] of turtle 0 => [0 1]
  map [[who] of ?] [ nw:turtles-on-path-to turtle 0 ] of turtle 1 => []

turtles-on-path-to-fully-connected
  extensions [ nw ]
  O> crt 6 [ create-links-with other turtles ]
  sort [length nw:turtles-on-path-to turtle 0 ] of turtles => [1 2 2 2 2 2]

turtles-on-path-to-on-a-circle
  extensions [ nw ]
  O> crt 6 [ create-link-with turtle ((who + 1) mod 6) ]
  sort [length nw:turtles-on-path-to turtle 0 ] of turtles => [1 2 2 3 3 4]

turtles-on-path-to-random-equal-paths
  extensions [ nw ]
  O> cro 4 [ create-link-with turtle ((who + 1) mod 4) ]
  length remove-duplicates n-values 100 [ [ nw:turtles-on-path-to turtle 2 ] of turtle 0 ] => 2

### mean-path-length

## undirected links

mean-path-length-empty
  extensions [ nw ]
  nw:mean-path-length => false

mean-path-length-one-turtle
  extensions [ nw ]
  nw:mean-path-length => false

mean-path-length-two-unconnected-turtles
  extensions [ nw ]
  O> crt 2
  nw:mean-path-length => false

mean-path-length-two-connected-turtles
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles ]
  nw:mean-path-length => 1

mean-path-length-two-connected-and-one-unconnected-turtles
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles ]
  O> crt 1
  nw:mean-path-length => false

mean-path-length-fully-connected
  extensions [ nw ]
  O> crt 10 [ create-links-with other turtles ]
  nw:mean-path-length => 1

mean-path-length-on-a-pentagon
  extensions [ nw ]
  O> crt 5 [ create-link-with turtle ((who + 1) mod 5) ]
  nw:mean-path-length => 1.5

mean-path-length-connected-pair-of-turtles-on-a-pentagon
  extensions [ nw ]
  O> nw:set-context mice links
  O> crt 5 [ create-link-with turtle ((who + 1) mod 5) ]
  O> ask (turtle-set turtle 0 turtle 1) [ set breed mice ]
  nw:mean-path-length => 1

mean-path-length-disconnected-pair-of-turtles-on-a-pentagon
  extensions [ nw ]
  O> nw:set-context mice links
  O> crt 5 [ create-link-with turtle ((who + 1) mod 5) ]
  O> ask (turtle-set turtle 0 turtle 2) [ set breed mice ]
  nw:mean-path-length => false

## directed links

mean-path-length-directed-two-half-connected-turtles
  extensions [ nw ]
  O> crt 2
  O> ask turtle 0 [ create-links-to other turtles ]
  nw:mean-path-length => false

mean-path-length-directed-two-connected-turtles
  extensions [ nw ]
  O> crt 2 [ create-links-to other turtles ]
  nw:mean-path-length => 1

## centrality primitives

### closeness-centrality

closeness-centrality-empty
  extensions [ nw ]
  [ nw:closeness-centrality ] of turtles => []

closeness-centrality-single-isolate
  extensions [ nw ]
  O> crt 1
  [ nw:closeness-centrality ] of turtle 0 => 0

closeness-centrality-simple-undirected
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ create-link-with turtle 2 ]
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => [0.67 1 0.67]

closeness-centrality-simple-directed
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-directed-link-to turtle 1 ]
  O> ask turtle 1 [ create-directed-link-to turtle 0 ]
  O> ask turtle 1 [ create-directed-link-to turtle 2 ]
  O> ask turtle 2 [ create-directed-link-to turtle 1 ]
  O> nw:set-context turtles directed-links
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => [0.67 1 0.67]

closeness-centrality-bigger-directed
  extensions [ nw ]
  O> crt 6
  O> ask turtle 0 [ create-directed-link-to turtle 1 ]
  O> ask turtle 1 [ create-directed-link-to turtle 5 ]
  O> ask turtle 2 [ create-directed-link-to turtle 1 ]
  O> ask turtle 2 [ create-directed-link-to turtle 3 ]
  O> ask turtle 2 [ create-directed-link-to turtle 4 ]
  O> ask turtle 4 [ create-directed-link-to turtle 3 ]
  O> ask turtle 5 [ create-directed-link-to turtle 0 ]
  O> ask turtle 5 [ create-directed-link-to turtle 4 ]
  O> nw:set-context turtles directed-links
  map [ [ precision nw:closeness-centrality 3 ] of ? ] sort turtles => [0.4 0.5 0.625 0 1 0.667]

closeness-centrality-disconnected-pentagon-triangle
  extensions [ nw ]
  O> crt 5 [ create-link-with turtle ((who + 1) mod 5) ]
  O> crt 3 [ create-link-with turtle (((who - 4) mod 3) + 5)]
  O> ask turtle 5 [ create-link-with turtle 6 ]
  O> ask turtle 6 [ create-link-with turtle 7 ]
  O> ask turtle 7 [ create-link-with turtle 5 ]
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => [0.67 0.67 0.67 0.67 0.67 1 1 1]

### betweenness-centrality

betweenness-centrality-empty
  extensions [ nw ]
  [ nw:betweenness-centrality ] of turtles => []

betweenness-centrality-single-isolate
  extensions [ nw ]
  O> crt 1
  [ nw:betweenness-centrality ] of turtle 0 => 0

betweenness-centrality-simple-undirected
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ create-link-with turtle 2 ]
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [0 1 0]

betweenness-centrality-disconnected-undirected
  extensions [ nw ]
  O> crt 6
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ create-link-with turtle 2 ]
  O> ask turtle 3 [ create-link-with turtle 4 ]
  O> ask turtle 4 [ create-link-with turtle 5 ]
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [0 1 0 0 1 0]

betweenness-centrality-directed-triangle
  extensions [ nw ]
  O> crt 3 [ create-link-to turtle ((who + 1) mod 3) ]
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [1 1 1]

betweenness-centrality-disconnected-directed-triangles
  extensions [ nw ]
  O> crt 3 [ create-link-to turtle ((who + 1) mod 3) ]
  O> crt 3 [ create-link-to turtle (((who - 2) mod 3) + 3) ]
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [1 1 1 1 1 1]

betweenness-centrality-connected-directed-triangles
  extensions [ nw ]
  O> crt 3 [ create-link-to turtle ((who + 1) mod 3) ]
  O> crt 3 [ create-link-to turtle (((who - 2) mod 3) + 3) ]
  O> ask turtle 0 [ create-link-to turtle 3 ]
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [7 1 4 7 4 1]

betweenness-centrality-bigger-connected-directed-graph
  extensions [ nw ]
  O> crt 6
  O> ask turtle 0 [ create-link-to turtle 4 ]
  O> ask turtle 1 [ create-link-to turtle 4 ]
  O> ask turtle 2 [ create-link-to turtle 0 ]
  O> ask turtle 2 [ create-link-to turtle 4 ]
  O> ask turtle 2 [ create-link-to turtle 1 ]
  O> ask turtle 4 [ create-link-to turtle 3 ]
  O> ask turtle 4 [ create-link-to turtle 0 ]
  O> ask turtle 4 [ create-link-to turtle 5 ]
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [0 0 0 0 7 0]

### eigenvector-centrality

eigenvector-centrality-empty
  extensions [ nw ]
  [ nw:eigenvector-centrality ] of turtles => []

eigenvector-centrality-single-isolate
  extensions [ nw ]
  O> crt 1
  [ nw:eigenvector-centrality ] of turtle 0 => 1

## weighted primitives

### weighted-distance-to

weighted-distance-to-between-two-turtles
  extensions [ nw ]
  O> crt 2
  O> ask turtle 0 [ create-undirected-link-with turtle 1 [ set weight 0.5 ] ]
  O> nw:set-context turtles undirected-links
  [ nw:weighted-distance-to turtle 1 "weight" ] of turtle 0 => 0.5

weighted-distance-to-variable-name-case-should-not-matter
  extensions [ nw ]
  O> crt 2
  O> ask turtle 0 [ create-undirected-link-with turtle 1 [ set weight 0.5 ] ]
  O> nw:set-context turtles undirected-links
  [ nw:weighted-distance-to turtle 1 "WEIGHT" ] of turtle 0 => 0.5

weighted-distance-to-self
  extensions [nw]
  O> crt 1
  O> nw:set-context turtles undirected-links
  [ nw:weighted-distance-to turtle 0 "weight" ] of turtle 0 => 0

weighted-distance-two-turtles-no-links
  extensions [nw]
  O> crt 2
  O> nw:set-context turtles undirected-links
  [ nw:weighted-distance-to turtle 0 "weight" ] of turtle 1 => false
  [ nw:weighted-distance-to turtle 1 "weight" ] of turtle 0 => false

weighted-distance-one-unbreeded-undirected-link-using-thickness
  extensions [nw]
  O> crt 2 [ create-links-with other turtles [ set thickness 0.5 ]]
  [ nw:weighted-distance-to turtle 0 "thickness" ] of turtle 1 => 0.5
  [ nw:weighted-distance-to turtle 1 "thickness" ] of turtle 0 => 0.5

weighted-distance-one-unbreeded-directed-link-using-thickness
  extensions [nw]
  O> crt 2
  O> ask turtle 0 [ create-link-to turtle 1 [ set thickness 0.5 ]]
  [ nw:weighted-distance-to turtle 0 "thickness" ] of turtle 1 => false
  [ nw:weighted-distance-to turtle 1 "thickness" ] of turtle 0 => 0.5

weighted-distance-changes
  extensions [nw]
  O> crt 2 [ create-links-with other turtles ]
  O> ask links [ set thickness 2 ]
  [ nw:weighted-distance-to turtle 1 "thickness" ] of turtle 0 => 2
  O> ask links [ set thickness 4 ]
  [ nw:weighted-distance-to turtle 1 "thickness" ] of turtle 0 => 4


### weighted-path-to

weighted-path-to-self
  extensions [ nw ]
  O> crt 1
  O> nw:set-context turtles undirected-links
  [ nw:weighted-path-to turtle 0 "weight" ] of turtle 0 => []

weighted-path-to-no-path
  extensions [ nw ]
  O> crt 2
  O> nw:set-context turtles undirected-links
  [ nw:weighted-path-to turtle 1 "weight" ] of turtle 0 => []
  [ nw:weighted-path-to turtle 0 "weight" ] of turtle 1 => []

weighted-path-to-undirected-pair
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles [ set thickness 1 ]]
  (word [ nw:weighted-path-to turtle 1 "thickness" ] of turtle 0) => "[(link 0 1)]"
  (word [ nw:weighted-path-to turtle 0 "thickness" ] of turtle 1) => "[(link 0 1)]"

weighted-path-to-random-equal-paths
  extensions [ nw ]
  O> cro 4 [ create-link-with turtle ((who + 1) mod 4) [ set thickness 1 ] ]
  length remove-duplicates n-values 100 [ [ nw:weighted-path-to turtle 2 "thickness" ] of turtle 0 ] => 2

### turtles-on-weighted-path-to

turtles-on-weighted-path-to-random-equal-paths
  extensions [ nw ]
  O> cro 4 [ create-link-with turtle ((who + 1) mod 4) [ set thickness 1 ] ]
  length remove-duplicates n-values 100 [ [ nw:turtles-on-weighted-path-to turtle 2 "thickness" ] of turtle 0 ] => 2

### mean-weighted-path-length

## generators

generate-random-undirected-fully-connected
  extensions [ nw ]
  O> nw:generate-random turtles links 2 1.0
  (word sort links) =>  "[(link 0 1)]"

generate-random-undirected-unconnected
  extensions [ nw ]
  O> nw:generate-random turtles links 2 0.0
  (word sort links) =>  "[]"

generate-random-directed-fully-connected
  extensions [ nw ]
  O> nw:generate-random turtles directed-links 2 1.0
  (word sort directed-links) =>  "[(directed-link 0 1) (directed-link 1 0)]"

generate-random-directed-unconnected
  extensions [ nw ]
  O> nw:generate-random turtles directed-links 2 0.0
  (word sort directed-links) =>  "[]"

## weak-component-clusterer

weak-components-empty-graph
  extensions [ nw ]
  (word nw:weak-component-clusters) => "[]"

weak-components-single-isolate
  extensions [ nw ]
  O> crt 1
  (word map [[who] of ?] nw:weak-component-clusters) => "[[0]]"

weak-components-two-isolates
  extensions [ nw ]
  O> crt 2
  (word sort (map [(word sort [who] of turtle-set ?)] nw:weak-component-clusters)) => "[[0] [1]]"

weak-components-connected-undirected-pair-and-isolate
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-undirected-link-with turtle 1 ]
  O> nw:set-context turtles undirected-links
  length nw:weak-component-clusters => 2
  (word sort (map [(word sort [who] of turtle-set ?)] nw:weak-component-clusters)) => "[[0 1] [2]]"
  O> nw:set-context turtles (link-set undirected-links)
  length nw:weak-component-clusters => 2
  (word sort (map [(word sort [who] of turtle-set ?)] nw:weak-component-clusters)) => "[[0 1] [2]]"

weak-components-connected-directed-pair-and-isolate
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-directed-link-to turtle 1 ]
  O> nw:set-context turtles directed-links
  length nw:weak-component-clusters => 2
  (word sort (map [(word sort [who] of turtle-set ?)] nw:weak-component-clusters)) => "[[0 1] [2]]"
  O> nw:set-context turtles (link-set directed-links)
  length nw:weak-component-clusters => 2
  (word sort (map [(word sort [who] of turtle-set ?)] nw:weak-component-clusters)) => "[[0 1] [2]]"

weak-components-two-connected-undirected-pairs
  extensions [ nw ]
  O> crt 4
  O> ask turtle 0 [ create-undirected-link-with turtle 1 ]
  O> ask turtle 2 [ create-undirected-link-with turtle 3 ]
  O> nw:set-context turtles undirected-links
  length nw:weak-component-clusters => 2
  (word sort (map [(word sort [who] of turtle-set ?)] nw:weak-component-clusters)) => "[[0 1] [2 3]]"
  O> nw:set-context turtles (link-set undirected-links)
  length nw:weak-component-clusters => 2
  (word sort (map [(word sort [who] of turtle-set ?)] nw:weak-component-clusters)) => "[[0 1] [2 3]]"

weak-components-two-connected-directed-pairs
  extensions [ nw ]
  O> crt 4
  O> ask turtle 0 [ create-directed-link-to turtle 1 ]
  O> ask turtle 2 [ create-directed-link-to turtle 3 ]
  O> nw:set-context turtles directed-links
  length nw:weak-component-clusters => 2
  (word sort (map [(word sort [who] of turtle-set ?)] nw:weak-component-clusters)) => "[[0 1] [2 3]]"
  O> nw:set-context turtles (link-set directed-links)
  length nw:weak-component-clusters => 2
  (word sort (map [(word sort [who] of turtle-set ?)] nw:weak-component-clusters)) => "[[0 1] [2 3]]"

weak-components-three-turtles-connected-by-two-link-breeds
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-undirected-link-with turtle 1 ]
  O> ask turtle 1 [ create-directed-link-to turtle 2 ]
  O> nw:set-context turtles undirected-links
  length nw:weak-component-clusters => 2
  O> nw:set-context turtles (link-set undirected-links)
  length nw:weak-component-clusters => 2
  O> nw:set-context turtles directed-links
  length nw:weak-component-clusters => 2
  O> nw:set-context turtles (link-set directed-links)
  length nw:weak-component-clusters => 2
  O> nw:set-context turtles links
  length nw:weak-component-clusters => 1
  O> nw:set-context turtles (link-set links)
  length nw:weak-component-clusters => 1

# clique tests

biggest-maximal-clique-undirected
  extensions [ nw ]
  O> crt 3 [ create-links-with other turtles ]
  count first nw:biggest-maximal-cliques => 3

biggest-maximal-clique-directed
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-link-to turtle 1 ]
  O> ask turtle 1 [ create-link-to turtle 2 ]
  O> ask turtle 2 [ create-link-to turtle 0 ]
  count first nw:biggest-maximal-cliques => ERROR Extension exception: Current graph must be undirected

# graphml tests

graphml-export-import-turtles
  extensions [ nw ]
  O> cro 2
  O> ask turtle 0 [ set tvar "a" ]
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ set tvar "b" ]
  O> ask link 0 1 [ set thickness 0.5 ]
  O> nw:save-graphml "extensions/nw/test/tmp/graphml-export-import-turtles.xml"
  O> ca
  O> nw:load-graphml "extensions/nw/test/tmp/graphml-export-import-turtles.xml"
  sort [ color ] of turtles => [5 15]
  sort [ tvar ] of turtles => ["a" "b"]
  [ thickness ] of link 0 1 => 0.5

graphml-export-import-breeds
  extensions [ nw ]
  O> nw:set-context mice links
  O> create-ordered-mice 2
  O> ask mouse 0 [ set fur "a" ]
  O> ask mouse 0 [ create-undirected-link-with mouse 1 ]
  O> ask mouse 1 [ set fur "b" ]
  O> ask undirected-link 0 1 [ set weight 0.5 ]
  O> nw:save-graphml "extensions/nw/test/tmp/graphml-export-import-breeds.xml"
  O> ca
  O> nw:load-graphml "extensions/nw/test/tmp/graphml-export-import-breeds.xml"
  sort [ color ] of mice => [5 15]
  sort [ fur ] of mice => ["a" "b"]
  [ weight ] of undirected-link 0 1 => 0.5

save-graphml-first-turtle-double
  extensions [ nw ]
  O> crt 2
  O> ask turtle 0 [ set tvar 1 ]
  O> ask turtle 1 [ set tvar "a" ]
  O> nw:save-graphml "extensions/nw/test/tmp/save-graphml-first-turtle-double.graphml"
  O> ca
  O> nw:load-graphml "extensions/nw/test/tmp/save-graphml-first-turtle-double.graphml"
  sort-by is-number? [ tvar ] of turtles => [1 "a"]

save-graphml-first-turtle-string
  extensions [ nw ]
  O> crt 2
  O> ask turtle 0 [ set tvar "a" ]
  O> ask turtle 1 [ set tvar 1 ]
  O> nw:save-graphml "extensions/nw/test/tmp/save-graphml-first-turtle-string.graphml"
  O> ca
  O> nw:load-graphml "extensions/nw/test/tmp/save-graphml-first-turtle-string.graphml"
  sort [ tvar ] of turtles => ["1" "a"]

load-graphml-missing-attr
  extensions [ nw ]
  O> nw:load-graphml "extensions/nw/test/missing-attr.graphml"
  [ heading ] of turtle 0 => 0
  [ color ] of turtle 0 => 5

load-graphml-missing-attr-name
  extensions [ nw ]
  O> nw:load-graphml "extensions/nw/test/missing-attr-name.graphml"
  [ heading ] of turtle 0 => 0
  [ color ] of turtle 0 => 5

load-graphml-missing-attr-type
  extensions [ nw ]
  O> nw:load-graphml "extensions/nw/test/missing-attr-type.graphml"
  [ heading ] of turtle 0 => 0
  [ color ] of turtle 0 => 5

