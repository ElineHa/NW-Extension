# Ideally, for each primitive, we should think about cases such as:
# - Parameter values: negative, 0, 1, possible maximums (e.g. 1E38)
# - same input twice (e.g. distance to self)
# Different network configurations:
# - empty networks
# - partially disconnected networks
# - completely disconnect networks
# - Directed vs. undirected networks
#   - using undirected primitives with directed networks
#   - using directed versions of primitives on undirected networks
# For primitives using weights:
# - non numeric weights
# - negative weights

### turtles-in-radius

turtles-in-radius-negative-radius
  extensions [nw]
  O> crt 1
  O> nw:set-snapshot turtles links
  [nw:turtles-in-radius -1] of turtle 0 => ERROR Extension exception: radius cannot be negative

turtles-in-radius-zero-radius
  extensions [nw]
  O> crt 5
  O> nw:set-snapshot turtles links
  remove-duplicates [count nw:turtles-in-radius 0] of turtles => [1]

turtles-in-radius-two-circles
  extensions [nw]
  O> crt 8 [ create-link-with turtle ((who + 1) mod 8) ]
  O> crt 8 [ create-link-with turtle ((who - 8 + 1) mod 8 + 8) ]
  O> nw:set-snapshot turtles links
  remove-duplicates [count nw:turtles-in-radius 0] of turtles => [1]
  remove-duplicates [count nw:turtles-in-radius 1 ] of turtles => [3]
  remove-duplicates [count nw:turtles-in-radius 2 ] of turtles => [5]
  remove-duplicates [count nw:turtles-in-radius 3 ] of turtles => [7]
  remove-duplicates [count nw:turtles-in-radius 4 ] of turtles => [8]
  remove-duplicates [count nw:turtles-in-radius 5 ] of turtles => [8]
  # Turned off because much too slow (see Issue #42 on github):
  #remove-duplicates [count nw:turtles-in-radius 1E38 ] of turtles => [8]

in-link-radius-source-set-filtering
  extensions [nw]
  O> crt 8 [ create-links-with other turtles ]
  O> set glob1 turtles with [who mod 2 = 0]
  O> nw:set-snapshot glob1 links
  sort [who] of [nw:turtles-in-radius 10] of turtle 0 => [0 2 4 6]

### turtles-in-out-radius, turtles-in-in-radius

turtles-in-out-radius
  extensions [nw]
  O> crt 2
  O> ask turtle 0 [ create-link-to turtle 1 ]
  O> nw:set-snapshot turtles links
  sort [ who ] of [ nw:turtles-in-out-radius 1 ] of turtle 0 => [0 1]
  sort [ who ] of [ nw:turtles-in-out-radius 1 ] of turtle 1 => [1]

turtles-in-in-radius
  extensions [nw]
  O> crt 2
  O> ask turtle 0 [ create-link-to turtle 1 ]
  O> nw:set-snapshot turtles links
  sort [who] of [ nw:turtles-in-in-radius 1 ] of turtle 0 => [0]
  sort [who] of [ nw:turtles-in-in-radius 1 ] of turtle 1 => [0 1]

### distance-to

distance-to-self
  extensions [nw]
  O> crt 1
  O> nw:set-snapshot turtles links
  [ nw:distance-to turtle 0 ] of turtle 0 => 0

distance-two-turtles-no-links
  extensions [nw]
  O> crt 2
  O> nw:set-snapshot turtles links
  [ nw:distance-to turtle 0 ] of turtle 1 => false
  [ nw:distance-to turtle 1 ] of turtle 0 => false

distance-one-undirected-link
  extensions [nw]
  O> crt 2 [ create-links-with other turtles ]
  O> nw:set-snapshot turtles links
  [ nw:distance-to turtle 0 ] of turtle 1 => 1
  [ nw:distance-to turtle 1 ] of turtle 0 => 1

distance-one-directed-link
  extensions [nw]
  O> crt 2
  O> ask turtle 0 [ create-link-to turtle 1 ]
  O> nw:set-snapshot turtles links
  [ nw:distance-to turtle 0 ] of turtle 1 => false
  [ nw:distance-to turtle 1 ] of turtle 0 => 1

distance-always-one-in-fully-connected-network
  extensions [ nw ]
  to-report distances report [ nw:distance-to myself ] of link-neighbors end
  O> crt 10 [ create-links-with other turtles ]
  O> nw:set-snapshot turtles links
  remove-duplicates reduce sentence [distances] of turtles => [1]

distance-along-chain
  extensions [ nw ]
  O> crt 5 [ if who < 4 [ create-link-with turtle (who + 1) ] ]
  O> nw:set-snapshot turtles links
  sort [ nw:distance-to turtle 0 ] of turtles => [0 1 2 3 4]
  sort [ nw:distance-to turtle 4 ] of turtles => [0 1 2 3 4]
  sort remove-duplicates [ nw:distance-to turtle 2 ] of turtles => [0 1 2]

distance-around-circle
  extensions [ nw ]
  O> crt 8 [ create-link-with turtle ((who + 1) mod 8) ]
  O> nw:set-snapshot turtles links
  sort [ nw:distance-to turtle 0 ] of turtles => [0 1 1 2 2 3 3 4]

distance-around-two-circles
  extensions [ nw ]
  O> crt 8 [ create-link-with turtle ((who + 1) mod 8) ]
  O> crt 8 [ create-link-with turtle ((who - 8 + 1) mod 8 + 8) ]
  O> nw:set-snapshot turtles links
  sort [ nw:distance-to turtle 0 ] of turtles with [who < 8] => [0 1 1 2 2 3 3 4]
  sort [ nw:distance-to turtle 8 ] of turtles with [who >= 8] => [0 1 1 2 2 3 3 4]
  remove-duplicates [ nw:distance-to turtle 8 ] of turtles with [who < 8] => [false]
  remove-duplicates [ nw:distance-to turtle 0 ] of turtles with [who >= 8] => [false]

### path-to

path-to-self
  extensions [ nw ]
  O> crt 1
  O> nw:set-snapshot turtles links
  [ nw:path-to turtle 0 ] of turtle 0 => []

path-to-no-path
  extensions [ nw ]
  O> crt 2
  O> nw:set-snapshot turtles links
  [ nw:path-to turtle 1 ] of turtle 0 => []
  [ nw:path-to turtle 0 ] of turtle 1 => []

path-to-undirected-pair
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles ]
  O> nw:set-snapshot turtles links
  (word [ nw:path-to turtle 1 ] of turtle 0) => "[(link 0 1)]"
  (word [ nw:path-to turtle 0 ] of turtle 1) => "[(link 0 1)]"

path-to-a-b-c-undirected
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ create-link-with turtle 2 ]
  O> nw:set-snapshot turtles links
  (word [ nw:path-to turtle 2 ] of turtle 0) => "[(link 0 1) (link 1 2)]"
  (word [ nw:path-to turtle 0 ] of turtle 2) => "[(link 1 2) (link 0 1)]"

path-to-a-b-c-directed
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-link-to turtle 1 ]
  O> ask turtle 1 [ create-link-to turtle 2 ]
  O> nw:set-snapshot turtles links
  (word [ nw:path-to turtle 2 ] of turtle 0) => "[(link 0 1) (link 1 2)]"
  (word [ nw:path-to turtle 0 ] of turtle 2) => "[]"

path-to-fully-connected
  extensions [ nw ]
  O> crt 6 [ create-links-with other turtles ]
  O> nw:set-snapshot turtles links
  (word sort map [(word ?)] [ nw:path-to turtle 0 ] of turtles) => "[[(link 0 1)] [(link 0 2)] [(link 0 3)] [(link 0 4)] [(link 0 5)] []]"

path-to-on-a-circle
  extensions [ nw ]
  O> crt 6 [ create-link-with turtle ((who + 1) mod 6) ]
  O> nw:set-snapshot turtles links
  sort [ length nw:path-to turtle 0 ] of turtles => [0 1 1 2 2 3]

### turtles-on-path-to

turtles-on-path-to-self
  extensions [ nw ]
  O> crt 1
  O> nw:set-snapshot turtles links
  map [[who] of ?] [ nw:turtles-on-path-to turtle 0 ] of turtle 0 => [0]

turtles-on-path-to-no-path
  extensions [ nw ]
  O> crt 2
  O> nw:set-snapshot turtles links
  map [[who] of ?] [ nw:turtles-on-path-to turtle 1 ] of turtle 0 => []
  map [[who] of ?] [ nw:turtles-on-path-to turtle 0 ] of turtle 1 => []

turtles-on-path-to-undirected-pair
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles ]
  O> nw:set-snapshot turtles links
  map [[who] of ?] [ nw:turtles-on-path-to turtle 1 ] of turtle 0 => [0 1]
  map [[who] of ?] [ nw:turtles-on-path-to turtle 0 ] of turtle 1 => [1 0]

turtles-on-path-to-fully-connected
  extensions [ nw ]
  O> crt 6 [ create-links-with other turtles ]
  O> nw:set-snapshot turtles links
  sort [length nw:turtles-on-path-to turtle 0 ] of turtles => [1 2 2 2 2 2]

turtles-on-path-to-on-a-circle
  extensions [ nw ]
  O> crt 6 [ create-link-with turtle ((who + 1) mod 6) ]
  O> nw:set-snapshot turtles links
  sort [length nw:turtles-on-path-to turtle 0 ] of turtles => [1 2 2 3 3 4]

### mean-path-length

## undirected links

mean-path-length-empty
  extensions [ nw ]
  O> nw:set-snapshot turtles links
  nw:mean-path-length => false

mean-path-length-one-turtle
  extensions [ nw ]
  O> nw:set-snapshot turtles links
  nw:mean-path-length => false

mean-path-length-two-unconnected-turtles
  extensions [ nw ]
  O> crt 2
  O> nw:set-snapshot turtles links
  nw:mean-path-length => false

mean-path-length-two-connected-turtles
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles ]
  O> nw:set-snapshot turtles links
  nw:mean-path-length => 1

mean-path-length-fully-connected
  extensions [ nw ]
  O> crt 10 [ create-links-with other turtles ]
  O> nw:set-snapshot turtles links
  nw:mean-path-length => 1

mean-path-length-on-a-pentagon
  extensions [ nw ]
  O> crt 5 [ create-link-with turtle ((who + 1) mod 5) ]
  O> nw:set-snapshot turtles links
  nw:mean-path-length => 1.5

mean-path-length-pairs-of-turtles-on-a-pentagon
  extensions [ nw ]
  O> crt 5 [ create-link-with turtle ((who + 1) mod 5) ]
  O> nw:set-snapshot (turtle-set turtle 0 turtle 1) links
  nw:mean-path-length => 1
  O> nw:set-snapshot (turtle-set turtle 1 turtle 0) links
  nw:mean-path-length => 1
  O> nw:set-snapshot (turtle-set turtle 0 turtle 2) links
  nw:mean-path-length => false
  O> nw:set-snapshot (turtle-set turtle 2 turtle 0) links
  nw:mean-path-length => false

## directed links

mean-path-length-directed-two-half-connected-turtles
  extensions [ nw ]
  O> crt 2
  O> ask turtle 0 [ create-links-to other turtles ]
  O> nw:set-snapshot turtles links
  nw:mean-path-length => false

mean-path-length-directed-two-connected-turtles
  extensions [ nw ]
  O> crt 2 [ create-links-to other turtles ]
  O> nw:set-snapshot turtles links
  nw:mean-path-length => 1

## centrality primitives

### closeness-centrality

closeness-centrality-empty
  extensions [ nw ]
  O> nw:set-snapshot turtles links
  [ nw:closeness-centrality ] of turtles => []

closeness-centrality-single-isolate
  extensions [ nw ]
  O> crt 1
  O> nw:set-snapshot turtles links
  [ nw:closeness-centrality ] of turtle 0 => 0

closeness-centrality-simple-undirected
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ create-link-with turtle 2 ]
  O> nw:set-snapshot turtles links
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => [0.67 1 0.67]

closeness-centrality-simple-directed
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-directed-link-to turtle 1 ]
  O> ask turtle 1 [ create-directed-link-to turtle 0 ]
  O> ask turtle 1 [ create-directed-link-to turtle 2 ]
  O> ask turtle 2 [ create-directed-link-to turtle 1 ]
  O> nw:set-snapshot turtles directed-links
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => [0.67 1 0.67]

closeness-centrality-bigger-directed
  extensions [ nw ]
  O> crt 6
  O> ask turtle 0 [ create-directed-link-to turtle 1 ]
  O> ask turtle 1 [ create-directed-link-to turtle 5 ]
  O> ask turtle 2 [ create-directed-link-to turtle 1 ]
  O> ask turtle 2 [ create-directed-link-to turtle 3 ]
  O> ask turtle 2 [ create-directed-link-to turtle 4 ]
  O> ask turtle 4 [ create-directed-link-to turtle 3 ]
  O> ask turtle 5 [ create-directed-link-to turtle 0 ]
  O> ask turtle 5 [ create-directed-link-to turtle 4 ]
  O> nw:set-snapshot turtles directed-links
  map [ [ precision nw:closeness-centrality 3 ] of ? ] sort turtles => [0.4 0.5 0.625 0 1 0.667] 

closeness-centrality-disconnected-pentagon-triangle
  extensions [ nw ]
  O> crt 5 [ create-link-with turtle ((who + 1) mod 5) ]
  O> crt 3 [ create-link-with turtle (((who - 4) mod 3) + 5)]
  O> ask turtle 5 [ create-link-with turtle 6 ]
  O> ask turtle 6 [ create-link-with turtle 7 ]
  O> ask turtle 7 [ create-link-with turtle 5 ]  
  O> nw:set-snapshot turtles links
  map [ [ precision nw:closeness-centrality 2 ] of ? ] sort turtles => [0.67 0.67 0.67 0.67 0.67 1 1 1]

### betweenness-centrality

betweenness-centrality-empty
  extensions [ nw ]
  O> nw:set-snapshot turtles links
  [ nw:betweenness-centrality ] of turtles => []

betweenness-centrality-single-isolate
  extensions [ nw ]
  O> crt 1
  O> nw:set-snapshot turtles links
  [ nw:betweenness-centrality ] of turtle 0 => 0

betweenness-centrality-simple-undirected
  extensions [ nw ]
  O> crt 3
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ create-link-with turtle 2 ]
  O> nw:set-snapshot turtles links
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [0 1 0]

betweenness-centrality-disconnected-undirected  
  extensions [ nw ]
  O> crt 6
  O> ask turtle 0 [ create-link-with turtle 1 ]
  O> ask turtle 1 [ create-link-with turtle 2 ]
  O> ask turtle 3 [ create-link-with turtle 4 ]
  O> ask turtle 4 [ create-link-with turtle 5 ]
  O> nw:set-snapshot turtles links
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [0 1 0 0 1 0]

betweenness-centrality-directed-triangle
  extensions [ nw ]
  O> crt 3 [ create-link-to turtle ((who + 1) mod 3) ]
  O> nw:set-snapshot turtles links
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [1 1 1]

betweenness-centrality-disconnected-directed-triangles
  extensions [ nw ]
  O> crt 3 [ create-link-to turtle ((who + 1) mod 3) ]
  O> crt 3 [ create-link-to turtle (((who - 2) mod 3) + 3) ]
  O> nw:set-snapshot turtles links
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [1 1 1 1 1 1]

betweenness-centrality-connected-directed-triangles
  extensions [ nw ]
  O> crt 3 [ create-link-to turtle ((who + 1) mod 3) ]
  O> crt 3 [ create-link-to turtle (((who - 2) mod 3) + 3) ]
  O> ask turtle 0 [ create-link-to turtle 3 ]
  O> nw:set-snapshot turtles links
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [7 1 4 7 4 1]

betweenness-centrality-bigger-connected-directed-graph    
  extensions [ nw ]
  O> crt 6
  O> ask turtle 0 [ create-link-to turtle 4 ]
  O> ask turtle 1 [ create-link-to turtle 4 ]
  O> ask turtle 2 [ create-link-to turtle 0 ]
  O> ask turtle 2 [ create-link-to turtle 4 ]
  O> ask turtle 2 [ create-link-to turtle 1 ]
  O> ask turtle 4 [ create-link-to turtle 3 ]
  O> ask turtle 4 [ create-link-to turtle 0 ]
  O> ask turtle 4 [ create-link-to turtle 5 ]    
  O> nw:set-snapshot turtles links
  map [ [ nw:betweenness-centrality ] of ? ] sort turtles => [0 0 0 0 7 0]

### eigenvector-centrality

eigenvector-centrality-empty
  extensions [ nw ]
  O> nw:set-snapshot turtles links
  [ nw:eigenvector-centrality ] of turtles => []

eigenvector-centrality-single-isolate
  extensions [ nw ]
  O> crt 1
  O> nw:set-snapshot turtles links
  [ nw:eigenvector-centrality ] of turtle 0 => 1

## weighted primitives

### weighted-distance-to

weighted-distance-to-between-two-turtles
  extensions [ nw ]
  O> crt 2
  O> ask turtle 0 [ create-undirected-link-with turtle 1 [ set weight 0.5 ] ]
  O> nw:set-snapshot turtles undirected-links
  [ nw:weighted-distance-to turtle 1 "weight" ] of turtle 0 => 0.5

weighted-distance-to-variable-name-case-should-not-matter
  extensions [ nw ]
  O> crt 2
  O> ask turtle 0 [ create-undirected-link-with turtle 1 [ set weight 0.5 ] ]
  O> nw:set-snapshot turtles undirected-links
  [ nw:weighted-distance-to turtle 1 "WEIGHT" ] of turtle 0 => 0.5

weighted-distance-to-self
  extensions [nw]
  O> crt 1
  O> nw:set-snapshot turtles undirected-links
  [ nw:weighted-distance-to turtle 0 "weight" ] of turtle 0 => 0

weighted-distance-two-turtles-no-links
  extensions [nw]
  O> crt 2
  O> nw:set-snapshot turtles undirected-links
  [ nw:weighted-distance-to turtle 0 "weight" ] of turtle 1 => false
  [ nw:weighted-distance-to turtle 1 "weight" ] of turtle 0 => false

weighted-distance-one-unbreeded-undirected-link-using-thickness
  extensions [nw]
  O> crt 2 [ create-links-with other turtles [ set thickness 0.5 ]]
  O> nw:set-snapshot turtles links
  [ nw:weighted-distance-to turtle 0 "thickness" ] of turtle 1 => 0.5
  [ nw:weighted-distance-to turtle 1 "thickness" ] of turtle 0 => 0.5

weighted-distance-one-unbreeded-directed-link-using-thickness
  extensions [nw]
  O> crt 2 
  O> ask turtle 0 [ create-link-to turtle 1 [ set thickness 0.5 ]]
  O> nw:set-snapshot turtles links
  [ nw:weighted-distance-to turtle 0 "thickness" ] of turtle 1 => false
  [ nw:weighted-distance-to turtle 1 "thickness" ] of turtle 0 => 0.5

### weighted-path-to

weighted-path-to-self
  extensions [ nw ]
  O> crt 1
  O> nw:set-snapshot turtles undirected-links
  [ nw:weighted-path-to turtle 0 "weight" ] of turtle 0 => []

weighted-path-to-no-path
  extensions [ nw ]
  O> crt 2
  O> nw:set-snapshot turtles undirected-links
  [ nw:weighted-path-to turtle 1 "weight" ] of turtle 0 => []
  [ nw:weighted-path-to turtle 0 "weight" ] of turtle 1 => []

weighted-path-to-undirected-pair
  extensions [ nw ]
  O> crt 2 [ create-links-with other turtles [ set thickness 1 ]]
  O> nw:set-snapshot turtles links
  (word [ nw:weighted-path-to turtle 1 "thickness" ] of turtle 0) => "[(link 0 1)]"
  (word [ nw:weighted-path-to turtle 0 "thickness" ] of turtle 1) => "[(link 0 1)]"

### turtles-on-weighted-path-to

### mean-weighted-path-length